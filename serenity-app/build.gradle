plugins {
  id 'project-report'
}

evaluationDependsOn(":emby-lib")
//evaluationDependsOn(':plexapp-rest-library')
evaluationDependsOn(':subtitle-converter')


apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: "kotlin-kapt"
apply plugin: "kotlin-allopen"
apply plugin: "jacoco"
apply plugin: 'org.sonarqube'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.gms.google-services'

sonarqube {
  properties {
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.organization", "kingargyle-github"
    property "sonar.login", "cb7af08f0b0e86306ac5365074326dc27aba503e"
    property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/testDebugUnitTest.exec"
//    property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/jacocoTestReport/jacocoTestReport.xml"
    property "sonar.coverage.exclusions", "**/R.class, **/R\$*.class, **/BuildConfig.*, **/Manifest*.*, **/*Test*.*,android/**/*.*, **/*MemberInjector.*, **/*\$\$Factory*.*, **/*..State*.*, **/*_ViewBinding*.*"
  }
}

allOpen {
  annotation("us.nineworlds.serenity.common.annotations.OpenForTesting")
}

android {
  defaultConfig {
    versionCode 3000000
    versionName "3.0.0-M1"
    minSdkVersion Versions.minSdkVersion
    targetSdkVersion Versions.targetSdkVersion
    multiDexEnabled true
    multiDexKeepProguard file("multidex_keep_file.txt")

    vectorDrawables.useSupportLibrary = true

    buildFeatures {
      viewBinding true
    }
  }

  aaptOptions {
    cruncherEnabled false
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
  }

  compileSdkVersion Versions.targetSdkVersion
  testOptions.unitTests.includeAndroidResources = true

  if (project.hasProperty("keystore")) {
    signingConfigs {
      release {
        storeFile file(project.property("keystore"))
        storePassword project.property("storepass")
        keyAlias project.property("keyalias")
        keyPassword project.property("keypass")
      }
    }
  }

  targetCompatibility = '1.8'
  sourceCompatibility = '1.8'

  buildTypes {
    debug {
      minifyEnabled false
      testCoverageEnabled false
    }
    release {
      if (project.hasProperty("keystore")) {
        signingConfig signingConfigs.release
      }
      zipAlignEnabled true
      minifyEnabled false
    }
  }

  lintOptions {
    checkReleaseBuilds false
    // Or, if you prefer, you can continue to check for errors in release builds,
    // but continue the build even when errors are found:
    abortOnError false
  }

  testOptions {
    unitTests.returnDefaultValues = true
    unitTests.includeAndroidResources = true
    unitTests.all {
      minHeapSize = "1024m"
      maxHeapSize = "1512m"
      forkEvery = 100
      maxParallelForks = 2

      jvmArgs '-noverify'

      testLogging {
        exceptionFormat "full"
      }
    }
  }

  compileOptions {
    incremental = true
    targetCompatibility 1.8
    sourceCompatibility 1.8
  }

  dexOptions {
    preDexLibraries = false
  }
}

dependencies {

  implementation platform('com.google.firebase:firebase-bom:28.4.1')


  implementation project(':subtitle-converter')
  implementation project(':emby-lib')
  implementation project(':serenity-android-common')
  implementation project(':serenity-common')

  implementation("androidx.recyclerview:recyclerview:1.2.1") {
    version {
      strictly "1.2.1"
    }
  }

  implementation 'com.google.firebase:firebase-analytics'
  implementation 'com.google.firebase:firebase-crashlytics'

  implementation group: 'com.github.bumptech.glide', name: 'okhttp3-integration', version: "$Versions.glideOkHttpVersion"
  implementation("com.google.android.exoplayer:exoplayer-core:$Versions.exoplayerVersion") {
    exclude module: 'support-annotations'
  }
  implementation("com.google.android.exoplayer:exoplayer-ui:$Versions.exoplayerVersion") {
    exclude module: 'support-annotations'
  }

  implementation("com.google.android.exoplayer:extension-okhttp:$Versions.exoplayerVersion") {
    exclude module: 'support-annotations'
  }

  implementation("com.squareup.okhttp3:okhttp:$Versions.okhttpVersion") {
    exclude group: 'com.android.support'
  }

  implementation("androidx.fragment:fragment-ktx:1.4.0")
  implementation 'com.google.android.material:material:1.4.0'
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$Versions.kotlinVersion"
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'
  implementation "com.github.moxy-community:moxy:$Versions.moxyVersion"
  implementation "com.github.moxy-community:moxy-app-compat:$Versions.moxyVersion"
  implementation "com.github.moxy-community:moxy-ktx:$Versions.moxyVersion"
  implementation "com.github.bumptech.glide:glide:$Versions.glideVersion"
  kapt "com.github.bumptech.glide:compiler:$Versions.glideVersion"
  implementation "org.greenrobot:eventbus:$Versions.eventBus"
  implementation("com.birbit:android-priority-jobqueue:$Versions.androidPriorityJobQueueVersion")
  implementation "androidx.appcompat:appcompat:1.3.1"
  implementation "androidx.leanback:leanback:1.1.0-rc02"
  implementation "androidx.leanback:leanback-preference:1.1.0-rc01"
  implementation "androidx.legacy:legacy-support-v4:1.0.0"
  implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
  implementation "androidx.percentlayout:percentlayout:1.0.0"
  implementation "androidx.annotation:annotation:1.2.0"
  implementation "com.googlecode.juniversalchardet:juniversalchardet:$Versions.universalCharDetVersion"
  implementation "com.squareup.okhttp3:logging-interceptor:$Versions.okhttpVersion"
  implementation "me.jessyan:retrofit-url-manager:$Versions.urlManager"
  implementation "com.squareup.okhttp3:okhttp-urlconnection:$Versions.okhttpVersion"
  implementation "com.jakewharton:butterknife:$Versions.butterKnifeVersion"
  implementation "com.jakewharton.timber:timber:$Versions.timberVersion"
  implementation "androidx.cardview:cardview:1.0.0"
  implementation "androidx.annotation:annotation:1.2.0"

  releaseImplementation("com.github.stephanenicolas.toothpick:toothpick-runtime:$Versions.toothPickVersion") {
    exclude group: 'javax.inject'
  }
  releaseImplementation("com.github.stephanenicolas.toothpick:smoothie:$Versions.toothPickVersion") {
    exclude group: 'javax.inject'
  }

  debugImplementation("com.github.stephanenicolas.toothpick:toothpick-runtime:$Versions.toothPickVersion")

  releaseImplementation "com.github.stephanenicolas.toothpick:toothpick-javax-annotations:$Versions.toothPickVersion"
  kapt "com.github.stephanenicolas.toothpick:toothpick-compiler:$Versions.toothPickVersion"

  implementation "com.squareup.moshi:moshi-kotlin:$Versions.moshiKotlinVersion"
  implementation "com.squareup.retrofit2:converter-moshi:$Versions.retrofitVersion"
  implementation "net.danlew:android.joda:$Versions.jodaTimeVersion"
  implementation "com.squareup.retrofit2:retrofit:$Versions.retrofitVersion"
  implementation 'com.google.android:flexbox:1.1.0'
  implementation 'com.henryblue.library:tvrecyclerview:1.2.2'
  implementation 'jp.wasabeef:recyclerview-animators:3.0.0'

  implementation('com.github.rstanic12:Resourceful:1.0.0') {
    exclude group: 'com.google.guava'
  }

  // https://mvnrepository.com/artifact/org.simpleframework/simple-xml
  implementation("org.simpleframework:simple-xml:$Versions.simpleXmlVersion") {
    exclude group: 'stax'
    exclude group: 'xpp3'
  }

  testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
  testImplementation "org.apache.commons:commons-lang3:$Versions.commonsLangVersion"
  testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.1'

  testImplementation "com.squareup.okhttp3:mockwebserver:$Versions.okhttpVersion"
  testImplementation "commons-io:commons-io:$Versions.commonsioVersion"
  testImplementation "com.github.stephanenicolas.toothpick:toothpick-testing:$Versions.toothPickVersion"
  testImplementation("org.robolectric:robolectric:$Versions.robolectricVersion") {
    exclude module: 'support-v4'
  }
  testImplementation("com.squareup.assertj:assertj-android:$Versions.assertJAndroidVersion") {
    exclude module: 'support-v4'
    exclude module: 'support-annotations'
  }

  testImplementation "com.willowtreeapps.assertk:assertk-jvm:$Versions.assertkVersion"
  testImplementation "org.assertj:assertj-core:$Versions.assertjVersion"
  testImplementation "junit:junit:$Versions.junitVersion"
  testImplementation "org.robolectric:shadows-framework:$Versions.robolectricVersion@jar"
  testImplementation "org.robolectric:shadowapi:$Versions.robolectricVersion"
  testImplementation "org.robolectric:shadows-playservices:$Versions.robolectricVersion"
  testImplementation "androidx.test:core:1.4.0"
  testImplementation "org.khronos:opengl-api:$Versions.openglApiVersion"
  testImplementation "org.mockito:mockito-core:$Versions.mockitoVersion"
  testImplementation 'androidx.test.ext:junit:1.1.3'

  kaptTest "com.github.stephanenicolas.toothpick:toothpick-compiler:$Versions.toothPickVersion"

  kapt "com.github.moxy-community:moxy-compiler:$Versions.moxyVersion"
  kapt "com.jakewharton:butterknife-compiler:$Versions.butterKnifeVersion"
}

//com.google.gms.googleservices.GoogleServicesPlugin.config.disableVersionCheck = true

jacoco {
   toolVersion = "0.8.7"
}

tasks.withType(Test) {
  jacoco.includeNoLocationClasses = true
  jacoco.excludes = ['jdk.internal.*']
}

task jacocoTestReport(type: JacocoReport, dependsOn: 'testDebugUnitTest') {

  reports {
    xml.enabled = true
    html.enabled = true
  }

  def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*', '**/*MemberInjector.*',
                    '**/*$$Factory*.*', '**/*..State*.*', '**/*_ViewBinding*.*', 'com/bumptech/**/*.*', '**/injection/**/*.*',
                    '**/*__Factory*.*', '**/*PresenterBinder*.*', '**/*ViewState*.*']
  def mainSrc = "${project.projectDir}/src/main/java"
  def kotlinSrc = "${project.projectDir}/src/main/kotlin"

  classDirectories.setFrom(fileTree(
          dir: "$buildDir/intermediates/javac/debug/classes",
          excludes: fileFilter) + fileTree(
          dir: "$buildDir/tmp/kotlin-classes/debug",
          excludes: fileFilter))

  // sources
  sourceDirectories.setFrom(files([
          mainSrc,
          kotlinSrc
  ]))

  executionData.setFrom(files("${buildDir}/jacoco/testDebugUnitTest.exec"))
}

configurations.all {
  resolutionStrategy {
    force 'androidx.fragment:fragment:1.4.0'
  }
}